plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
}

group = 'com.finestmedia'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {
    implementation(
            "org.springframework.boot:spring-boot-starter-data-rest",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.flywaydb:flyway-core",
            "org.mapstruct:mapstruct:1.5.1.Final",
            "io.springfox:springfox-boot-starter:3.0.0",
    )
    compileOnly(
            "org.projectlombok:lombok",
            "org.mapstruct:mapstruct:1.5.1.Final"
    )
    runtimeOnly(
            "org.postgresql:postgresql"
    )
    annotationProcessor(
            "org.springframework.boot:spring-boot-configuration-processor",
            "org.projectlombok:lombok",
            "org.mapstruct:mapstruct-processor:1.5.1.Final"
    )
    testImplementation(
            "org.springframework.boot:spring-boot-starter-test",
            "org.testcontainers:postgresql:1.17.2",
    )
}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
    compileOnly.extendsFrom annotationProcessor

    integrationImplementation.extendsFrom implementation, testImplementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
    integrationAnnotationProcessor.extendsFrom annotationProcessor
    integrationCompileOnly.extendsFrom compileOnly
}

idea {
    module {
        testSourceDirs += sourceSets.integration.allSource.srcDirs
    }
}

test {
    useJUnitPlatform { JUnitPlatformOptions options ->
        options.excludeTags 'integration'
    }
}

task integrationTests(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform { JUnitPlatformOptions options ->
        options.includeTags 'integration'
    }

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    shouldRunAfter test
}
